buildscript {
    dependencies {
        classpath "gradle.plugin.nl.javadude.gradle.plugins:license-gradle-plugin:0.13.1"
    }
}

plugins {
    /* this plugin can check for newer versions of the used dependencies.
     * run it from the command line like this:
     * ./gradlew dependencyUpdates
     * see also: https://github.com/ben-manes/gradle-versions-plugin
     *
     * in Android Studio open the gradle projects view and start
     * Tasks - help - dependencyUpdates
     *
     * alternative:
     * IntelliJ: Analyze - Inspect code
     *   the results in the Inspection view will contain dependencies that can be updated
     *   Android>Lint>Correctness>Obsolete Gradle Dependency
     */
    id 'com.github.ben-manes.versions' version '0.14.0'
}

group 'com.tmtron.enums'
version '1.0-SNAPSHOT'

subprojects {
    apply plugin: 'java'

    apply plugin: "com.github.hierynomus.license"
    license {
        header rootProject.file('LICENSE_HEADER')
        strictCheck true
        mapping {
            java = 'SLASHSTAR_STYLE'
        }
        ext.year = Calendar.getInstance().get(Calendar.YEAR)
    }

    /* create a jar for sources and java-docs
     * http://stackoverflow.com/a/11475089/6287240
     */
    task sourcesJar(type: Jar, dependsOn: classes) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }

    task javadocJar(type: Jar, dependsOn: javadoc) {
        classifier = 'javadoc'
        from javadoc.destinationDir
    }

    artifacts {
        archives sourcesJar
        archives javadocJar
    }
}

repositories {
    mavenCentral()
}

